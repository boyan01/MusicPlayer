if (isIndependentApk.toBoolean()) {
    apply plugin: 'com.android.application'
} else {
    apply plugin: 'com.android.library'
}

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

apply plugin: 'kotlin-kapt'


android {

    compileSdkVersion compile_sdk

    defaultConfig {
        minSdkVersion min_sdk.toInteger()
        targetSdkVersion target_sdk
        versionCode 1
        versionName "1.0"

        resourcePrefix project.getName()

        if (isIndependentApk.toBoolean()) {
            applicationId "tech.summerly.quiet." + project.getName()
        }

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        main {
            if (isIndependentApk.toBoolean()) {
                manifest.srcFile 'src/main/AndroidManifest_debug.xml'
            } else {
                manifest.srcFile 'src/main/AndroidManifest.xml'
                java {
                    exclude 'debug/**'
                }
            }
        }
    }

    compileOptions {
        targetCompatibility 1.7
        sourceCompatibility 1.7
    }
}

kotlin {
    experimental {
        coroutines "enable"
    }
}


kapt {
    arguments {
        arg("moduleName", project.getName())
    }
}


dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.1'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.1'
    kapt 'com.alibaba:arouter-compiler:1.1.4'

    implementation project(path: ':commonlib')
}