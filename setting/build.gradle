if (isIndependentApk.toBoolean()) {
    apply plugin: 'com.android.application'
} else {
    apply plugin: 'com.android.library'
}

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion compile_sdk.toInteger()
    defaultConfig {
        if (isIndependentApk.toBoolean()) {
            applicationId "tech.summerly.quiet.setting"
        }
        minSdkVersion min_sdk.toInteger()
        targetSdkVersion target_sdk.toInteger()
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        resourcePrefix "setting"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    sourceSets {
        main {
            if (isIndependentApk.toBoolean()) {
                manifest.srcFile 'src/main/AndroidManifest_debug.xml'
            } else {
                manifest.srcFile 'src/main/AndroidManifest.xml'
                java {
                    exclude "**/DebugPlayerActivity.class"
                    exclude 'debug/**'
                }
                res {
                    exclude "***/setting_activity_debug.xml"
                }
            }
        }
    }
}

kotlin {
    experimental {
        coroutines "enable"
    }
}

kapt {
    arguments {
        arg("moduleName", project.getName())
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation project(path: ':commonlib')
    kapt 'com.alibaba:arouter-compiler:1.1.4'

    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.1'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.1'
}
